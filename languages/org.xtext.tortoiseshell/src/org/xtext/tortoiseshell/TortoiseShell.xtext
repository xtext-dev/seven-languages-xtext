/*******************************************************************************
 * Copyright (c) 2012 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 ******************************************************************************/
grammar org.xtext.tortoiseshell.TortoiseShell with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xbase"
generate tortoiseShell "http://www.xtext.org/tortoiseshell/TortoiseShell"

Program :
	body=Body
	subPrograms+=SubProgram*;
	
SubProgram:
	'sub' name=ValidID (':' returnType=JvmTypeReference)?
	(parameters += FullJvmFormalParameter)*
	body=Body;

Body returns XBlockExpression:
	{XBlockExpression}
	'begin'
	(expressions+=XExpressionOrVarDeclaration ';'?)*
	'end';
	
Executable:
	Program | SubProgram;